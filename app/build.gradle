plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-android-extensions"
    id "kotlin-kapt"
}

android {
    compileSdkVersion 28

    // Read the Github access token from 'local.properties'
    String githubToken = ""
    def localProperties = project.rootProject.file('local.properties')
    if (localProperties.canRead()) {
        Properties p = new Properties()
        p.load(new FileInputStream(localProperties))
        githubToken = p['github.access.token'].toString()
    }

    defaultConfig {
        applicationId "com.subinkrishna.rxdemo"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'GITHUB_ACCESS_TOKEN', "\"$githubToken\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // AppCompat/Support
    def appCompatVersion = '28.0.0-alpha3'
    implementation "com.android.support:appcompat-v7:$appCompatVersion"
    implementation "com.android.support:recyclerview-v7:$appCompatVersion"

    // KTX, ViewModel and LiveData
    def ktxVersion = '1.6.0'
    def lifecycleVersion = '2.3.1'
    implementation "androidx.core:core-ktx:$ktxVersion"
    implementation "androidx.appcompat:appcompat:1.3.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"


    // UI
    implementation 'com.squareup.picasso:picasso:2.71828'

    // Rx
    implementation 'io.reactivex.rxjava2:rxjava:2.1.16'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    // Networking & JSON
    def okHttpVersion = '4.9.1'
    def retrofitVersion = '2.9.0'
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.google.code.gson:gson:2.8.6"

    // Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
